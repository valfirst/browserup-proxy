subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = 'com.github.valfirst.browserup-proxy'
    version = '3.0.2-SNAPSHOT'

    sourceCompatibility=11

    ext {
        bcpVersion = '1.80'
        guiceVersion = '5.1.0'
        jacksonVersion = '2.18.2'
        jerseyVersion = '2.46'
        jettyVersion = '9.4.56.v20240826'
        log4jVersion = '2.24.3'
        nettyVersion = '4.1.117.Final'
        littleProxyVersion = '2.4.0'
        slf4jVersion = '2.0.16'
        swaggerVersion = '2.2.26'
        guavaVersion = '33.4.0-jre'
        seleniumVersion = '4.28.1'
        hamcrestVersion = '3.0'
    }

    dependencies {
        modules {
            module('commons-logging:commons-logging') {
                replacedBy('org.slf4j:jcl-over-slf4j', 'Redirect commons-logging logs to SLF4J')
            }
        }
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/service/local/staging/deploy/maven2/" }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    signing {
        if (project.hasProperty("sign")) {
            sign configurations.archives
        }
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    test {
        testLogging {
            outputs.upToDateWhen {false}
            showStandardStreams = true
        }
        def mitmproxyBinaryPath = project.findProperty('MITMPROXY_BINARY_PATH')
        if (mitmproxyBinaryPath) {
            systemProperty "MITMPROXY_BINARY_PATH", mitmproxyBinaryPath
        }
    }
}
