subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    group = 'com.github.valfirst.browserup-proxy'
    version = '3.2.1-SNAPSHOT'

    sourceCompatibility=11

    ext {
        bcpVersion = '1.81'
        guiceVersion = '6.0.0'
        jacksonVersion = '2.19.1'
        jerseyVersion = '2.46'
        jettyVersion = '9.4.57.v20241219'
        log4jVersion = '2.25.0'
        nettyVersion = '4.2.2.Final'
        littleProxyVersion = '2.4.2'
        slf4jVersion = '2.0.17'
        swaggerVersion = '2.2.26'
        guavaVersion = '33.4.8-jre'
        seleniumVersion = '4.33.0'
        hamcrestVersion = '3.0'
    }

    dependencies {
        modules {
            module('commons-logging:commons-logging') {
                replacedBy('org.slf4j:jcl-over-slf4j', 'Redirect commons-logging logs to SLF4J')
            }
        }
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/service/local/staging/deploy/maven2/" }
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    test {
        testLogging {
            outputs.upToDateWhen {false}
            showStandardStreams = true
        }
        def mitmproxyBinaryPath = project.findProperty('MITMPROXY_BINARY_PATH')
        if (mitmproxyBinaryPath) {
            systemProperty "MITMPROXY_BINARY_PATH", mitmproxyBinaryPath
        }
    }
}
