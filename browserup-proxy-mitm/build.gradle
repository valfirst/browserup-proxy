plugins {
    id 'groovy'
    id 'java-library'
    id 'signing'
}

archivesBaseName = 'browserup-proxy-mitm'

publishing {
    repositories {
        maven {
            name 'Maven'
            url project.version.endsWith("-SNAPSHOT") ?
                'https://oss.sonatype.org/content/repositories/snapshots/' :
                'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {
                credentials {
                    username "$ossrhUsername"
                    password "$ossrhPassword"
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId "${project.group}"
            artifactId "${project.name}"
            from components.java

            pom {
                name = archivesBaseName
                description = 'BrowserUp Proxy - MITM'
                url = 'https://github.com/valfirst/browserup-proxy'
                packaging = 'jar'

                scm {
                    connection = 'scm:git:git@github.com:valfirst/browserup-proxy.git'
                    developerConnection = 'scm:git:git@github.com:valfirst/browserup-proxy.git'
                    url = 'https://github.com/valfirst/browserup-proxy/tree/main/browserup-proxy-mitm'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'valfirst'
                        name = 'Valery Yatsynovich'
                        email = 'valeryyatsynovich@gmail.com'
                    }
                }
            }
        }
    }
}

signing {
    if (project.hasProperty('sign')) {
        sign publishing.publications.mavenJava
    }
}

dependencies {
    implementation "com.google.guava:guava:${guavaVersion}"

    implementation "org.bouncycastle:bcpkix-jdk18on:${bcpVersion}"
    implementation "org.bouncycastle:bcprov-jdk18on:${bcpVersion}"

    implementation "io.netty:netty-all:${nettyVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation("io.github.littleproxy:littleproxy:${littleProxyVersion}") {
        exclude(group: 'io.netty', module: 'netty-all')
    }

    testImplementation 'junit:junit:4.13.2'
    testImplementation('org.apache.httpcomponents:httpclient:4.5.14') {
        exclude(module: 'commons-logging')
    }
    testImplementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    testImplementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    testImplementation "org.apache.logging.log4j:log4j-slf4j2-impl:${log4jVersion}"
    testImplementation "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.25'
    testImplementation "org.hamcrest:hamcrest:${hamcrestVersion}"
    testImplementation 'org.mockito:mockito-core:5.18.0'
}
