plugins {
    id 'application'
}

application {
    applicationDefaultJvmArgs = ['-DMITMPROXY_BINARY_PATH=mitmproxy/']
}

archivesBaseName = 'browserup-proxy'

publishing {
    repositories {
        maven {
            name 'Maven'
            url project.version.endsWith("-SNAPSHOT") ?
                'https://oss.sonatype.org/content/repositories/snapshots/' :
                'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {
                credentials {
                    username "$ossrhUsername"
                    password "$ossrhPassword"
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId "${project.group}"
            artifactId "${archivesBaseName}"

            from components.java
            artifact(sourcesJar)
            artifact(javadocJar)

            pom {
                name = archivesBaseName
                description = 'BrowserUp Proxy'
                url = 'https://github.com/valfirst/browserup-proxy'
                packaging = 'zip'

                scm {
                    connection = 'scm:git:git@github.com:valfirst/browserup-proxy.git'
                    developerConnection = 'scm:git:git@github.com:valfirst/browserup-proxy.git'
                    url = 'https://github.com/valfirst/browserup-proxy'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'valfirst'
                        name = 'Valery Yatsynovich'
                        email = 'valeryyatsynovich@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    implementation project(':browserup-proxy-core')
    implementation project(':browserup-proxy-rest')

    implementation "com.google.inject:guice:${guiceVersion}"
    implementation "com.google.inject.extensions:guice-servlet:${guiceVersion}"
    implementation "com.google.inject.extensions:guice-multibindings:${guiceVersion}"
    implementation('com.google.sitebricks:sitebricks:0.8.11') {
        exclude(group: 'org.jboss.netty', module: 'netty')
        exclude(module: 'validation-api')
    }
    implementation "io.netty:netty-all:${nettyVersion}"
    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-slf4j2-impl:${log4jVersion}"
    implementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
    implementation "org.eclipse.jetty:jetty-servlet:${jettyVersion}"
}

applicationName = 'browserup-proxy'
mainClassName = 'com.browserup.bup.proxy.Main'

def mitmproxyLinuxURL = 'https://mitmproxy-linux.s3.us-east-2.amazonaws.com/mitmdump'.toURL() // Returns HTTP 403
def mitmproxyWindowsURL = 'https://mitmproxy-windows.s3.us-east-2.amazonaws.com/mitmdump.exe'.toURL()

static void downloadFile(URL srcUrl, File dstFile) {
    dstFile.withOutputStream { out ->
        srcUrl.withInputStream { from ->
            out << from
        }
    }
}

task downloadMitmproxyBinaries {
    File mitmproxyDir = file("$buildDir/mitmproxy")
    outputs.dir mitmproxyDir
    doLast {
        mitmproxyDir.mkdirs()
        println("Downloading mitmproxy dependencies...")
        def linuxBinaryFile = file("$buildDir/mitmproxy/mitmdump")
        def windowsBinaryFile = file("$buildDir/mitmproxy/mitmdump.exe")
        downloadFile(mitmproxyLinuxURL, linuxBinaryFile)
        downloadFile(mitmproxyWindowsURL, windowsBinaryFile)
        project.exec {
            commandLine('chmod', '+x', linuxBinaryFile.absolutePath)
            commandLine('chmod', '+x', windowsBinaryFile.absolutePath)
        }
        println("Downloaded mitmproxy dependencies.")
        mitmproxyDir
    }
}

distributions {
    main {
        distributionBaseName = 'browserup-proxy'
        contents {
            from '../LICENSE'
            from '../README.md'
            into ('ssl') {
                from '../browserup-proxy-core/src/main/resources/sslSupport'
            }
            /* Temporarily disable downloading of mitmproxy binaries due to HTTP 403 error
            from(downloadMitmproxyBinaries) {
                into 'bin/mitmproxy'
            }*/
        }
    }
}
