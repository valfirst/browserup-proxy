plugins {
    id 'groovy'
    id 'java-library'
    id 'signing'
}

archivesBaseName = 'browserup-proxy-core'

publishing {
    repositories {
        maven {
            name 'Maven'
            url project.version.endsWith("-SNAPSHOT") ?
                'https://oss.sonatype.org/content/repositories/snapshots/' :
                'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {
                credentials {
                    username "$ossrhUsername"
                    password "$ossrhPassword"
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId "${project.group}"
            artifactId "${project.name}"
            from components.java

            pom {
                name = archivesBaseName
                description = 'BrowserUp Proxy - Core'
                url = 'https://github.com/valfirst/browserup-proxy'
                packaging = 'jar'

                scm {
                    connection = 'scm:git:git@github.com:valfirst/browserup-proxy.git'
                    developerConnection = 'scm:git:git@github.com:valfirst/browserup-proxy.git'
                    url = 'https://github.com/valfirst/browserup-proxy/tree/main/browserup-proxy-core'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'valfirst'
                        name = 'Valery Yatsynovich'
                        email = 'valeryyatsynovich@gmail.com'
                    }
                }
            }
        }
    }
}

signing {
    if (project.hasProperty('sign')) {
        sign publishing.publications.mavenJava
    }
}

dependencies {
    api "io.netty:netty-all:${nettyVersion}"
    api("io.github.littleproxy:littleproxy:${littleProxyVersion}") {
        exclude(group: 'io.netty', module: 'netty-all')
    }
    api 'de.sstoehr:har-reader:2.5.0'

    implementation project(':browserup-proxy-mitm')

    implementation 'org.awaitility:awaitility:4.3.0'
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "org.bouncycastle:bcpkix-jdk18on:${bcpVersion}"
    implementation "org.bouncycastle:bcprov-jdk18on:${bcpVersion}"
    implementation 'org.brotli:dec:0.1.2'
    implementation "org.seleniumhq.selenium:selenium-api:${seleniumVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.24'
    testImplementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    testImplementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    testImplementation "org.apache.logging.log4j:log4j-slf4j2-impl:${log4jVersion}"
    testImplementation "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    testImplementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
    testImplementation "org.eclipse.jetty:jetty-servlet:${jettyVersion}"
    testImplementation "org.eclipse.jetty:jetty-servlets:${jettyVersion}"
    testImplementation "org.hamcrest:hamcrest:${hamcrestVersion}"
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.0'
    testImplementation 'org.mockito:mockito-core:5.16.1'
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.14'
}
