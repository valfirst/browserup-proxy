plugins {
    id 'java-library'
    id 'groovy'
}

archivesBaseName = 'browserup-proxy-rest'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }

            pom.project {
                name 'browserup-proxy-rest'
                description 'BrowserUp Proxy - REST'
                url 'https://github.com/valfirst/browserup-proxy'
                packaging 'jar'

                scm {
                    connection 'scm:git:git@github.com:valfirst/browserup-proxy.git'
                    developerConnection 'scm:git:git@github.com:valfirst/browserup-proxy.git'
                    url 'https://github.com/valfirst/browserup-proxy/tree/master/browserup-proxy-rest'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'valfirst'
                        name 'Valery Yatsynovich'
                        email 'valeryyatsynovich@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    implementation project(':browserup-proxy-core')

    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "com.google.inject:guice:${guiceVersion}"
    implementation "com.google.inject.extensions:guice-servlet:${guiceVersion}"
    implementation "com.google.inject.extensions:guice-multibindings:${guiceVersion}"
    implementation('com.google.sitebricks:sitebricks:0.8.11') {
        exclude(group: 'org.jboss.netty', module: 'netty')
        exclude(module: 'validation-api')
    }
    implementation "io.netty:netty-all:${nettyVersion}"
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
    implementation "org.eclipse.jetty:jetty-servlet:${jettyVersion}"
    implementation "org.slf4j:jcl-over-slf4j:${slf4jVersion}"
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    implementation "org.glassfish.jersey.containers:jersey-container-servlet-core:${jerseyVersion}"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:${jerseyVersion}"
    implementation "org.glassfish.jersey.inject:jersey-hk2:${jerseyVersion}"
    implementation "org.glassfish.jersey.ext:jersey-bean-validation:${jerseyVersion}"

    implementation "io.swagger.core.v3:swagger-jaxrs2:${swaggerVersion}"
    implementation "io.swagger.core.v3:swagger-core:${swaggerVersion}"
    implementation "io.swagger.core.v3:swagger-integration:${swaggerVersion}"
    implementation "io.swagger.core.v3:swagger-jaxrs2-servlet-initializer:${swaggerVersion}"
    implementation "io.swagger.core.v3:swagger-models:${swaggerVersion}"

    testImplementation project(':browserup-proxy-mitm')

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    testImplementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    testImplementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    testImplementation "org.apache.groovy:groovy:${groovyVersion}"
    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'
    testImplementation "org.hamcrest:hamcrest:${hamcrestVersion}"
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation "org.seleniumhq.selenium:selenium-api:${seleniumVersion}"
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.33.2'
}

task createVersionProperties() {
    doLast {
        new File("$buildDir/resources/main/browserup-proxy-rest-version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

build.finalizedBy(createVersionProperties)
